import Gamepad;

class JoystickConfigScreen : AbstractScene {
	field bg;
	field next = this;
	field flags = '';
	field buttons = 'left right up down A B start'.split(' ');
	field phase = 0;
	field gamepad;
	field configs_taken = {};
	field config = {};
	
	constructor(gamepad, bg) : base() {
		this.bg = bg;
		this.bg.next = this.bg;
		this.gamepad = gamepad;
		
		gamepad.pushEmptyConfig();
		gamepad.clearBindings();
	}
	
	function processInput(events, pressed) { }
	
	function update() {
		if (this.phase < this.buttons.length) {
			if (this.gamepad.bindDigitalButton(this.buttons[this.phase])) {
				this.phase++;
			}
		}
		
		if (this.phase == this.buttons.length) {
			GamepadManager.saveSettingsToUserData();
			this.next = new TitleScene();
		}
	}
	
	function render(rc) {
		this.bg.render(rc);
		drawBox(40, 64, 20, 15);
		
		if (this.phase < this.buttons.length) {
			button = this.buttons[this.phase];
			drawText(255, 255, 255, "Press " + button, 40 + 16, 64 + 16);
			img_name = button.lower();
			if (((rc / 6) % 2) == 0)
				img_name = 'none';
			ImageLibrary.get('misc/nes_' + img_name + '.png').draw(40 + 16, 64 + 32);
		}
	}
}
