import GFX;
import Math;

const cred_BO = "BLAKE O'HARE";
const cred_TB = "TED BURTON";
const cred_SC = "STEVE CRAWFORD";
const cred_LF = "LAURA FREER";
const cred_CS = "CHRISTINE SANDQUIST";
const cred_AM = "ANGEL MCLAUGHLIN";
const cred_AC = "ADRIAN CLINE";

class CreditsScene {

	static field CREDITS = [
		['TEAM NERD PARADISE', []],
		['PROGRAMMING', [cred_BO]],
		['LEVEL DESIGN', [cred_TB, cred_SC, cred_LF]],
		['CHARACTER ART', [cred_CS]],
		['LEVEL ART', [cred_AM]],
		['MUSIC AND SFX', [cred_AC]],
		['DIALOG', [cred_LF]],
		['AND YOU!', ['FOR PLAYING', 'OR SOMETHING LIKE THAT']],
		['', ['THE END']]
	];

	field next = this;
	field flags = 'M';
	field counter = 0;
	field images;
	
	constructor() {
		images = [];
		y = 0;
		for (i = 0; i < CreditsScene.CREDITS.length; ++i) {
			images.add([CreditsScene.CREDITS[i][0], y]);
			
			y += 16;
			for (person : CreditsScene.CREDITS[i][1]) {
				images.add([person, y]);
				y += 8;
			}
			
			y += 64;
		}
		this.images = images;
	}
	
	function processInput(events, pressed) {
		if (this.counter > 900) {
			for (event : events) {
				if (event.down) {
					if (event.action == 'start' || event.action == 'A' || event.action == 'B') {
						this.next = new TitleScene();
					}
				}
			}
		}
	}
	
	function update() {
		this.playMusic();
		this.counter++;
	}
	
	function render(rc) {
		GFX.Draw.fill(0, 0, 0);
		
		counter = Math.min(this.counter, 892);
		
		for (image : this.images) {
			drawText(255, 255, 255, image[0], 64, image[1] - counter + 224);
		}
	}
	
	function playMusic() {
		JukeBox.ensureSong('credits');
	}
}
