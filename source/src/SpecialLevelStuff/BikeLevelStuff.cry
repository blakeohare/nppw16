import Graphics2D;
import Math;
import Random;

class Point {
	field x;
	field y;
	field vx;
	field vy;
	
	constructor() { }
}

class BikeLevelStuff : SpecialLevelStuff {
	field particles = [];
	field counter = 0;
	
	constructor(scene) : base(scene) { }
		
	function update() {
		if (this.counter == 0) {
			this.scene.sprites.add(new Sprite('bird2', 16, 48));
		}
		
		this.counter++;
		
		scene = this.scene;
		player = scene.player;
		if (player.x > (scene.cols - 1) * 16) { // one less because you can't technically go off screen
			scene.next = new VictorySequenceScene(scene);
		}
		
		if (this.counter % 2 == 0) {
			pt = new Point();
			pt.x = player.x;
			pt.y = player.y;
			pt.vx = Random.randomFloat() * 16 - 8;
			pt.vy = Random.randomFloat() * 6 - 5;
			this.particles.add(pt);
		}
		
		for (pt : this.particles) {
			pt.x += pt.vx;
			pt.y += pt.vy;
			pt.vy += 2;
		}
	}
	
	function render(rc, offsetX, offsetY) {
		p = this.scene.player;
		for (pt : this.particles) {
			Graphics2D.Draw.rectangle(floor(pt.x + offsetX), floor(pt.y + offsetY), 3, 3, 255, 255, 128);
		}
		
		if (this.particles.length > 40) {
			this.particles = this.particles[1:];
		}
	}
}
