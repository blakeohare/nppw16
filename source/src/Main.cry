import Game;
import Gamepad;
import Math;

const FPS = 30;
const WIDTH = 870;
const HEIGHT = 672;

const GAME_WIDTH = 256;
const GAME_HEIGHT = 224;
const DEBUG_MODE = false;
const JOYSTICKS_ENABLED = false;
const SOUND_ENABLED = true;

function main() {
	window = new Game.GameWindow('Space Squirrel', FPS, 256, 224, WIDTH, HEIGHT);
	GamepadManager.refreshDevices();
	GamepadManager.restoreSettingsFromUserData(['active']);
	
	activeScene = new OpeningScene();
	renderCounter = 0;

	pressedActions = {};
	for (action : 'left up down right A B start'.split(' ')) {
		pressedActions[action] = false;
	}
	
	events = [];
	while (activeScene != null) {
		events.clear();
		
		quitAttempt = false;
		raw_events = window.pumpEvents();
		for (event : raw_events) {
			if (event.is_quit) {
				return;
			} else {
				down = false;
				action = null;
				if (event.is_gamepad) {
					action = event.name;
					down = event.value;
					
				} else if (event.is_key) {
					down = event.down;
					action = null;
					switch (event.key) {
						case 'enter': action = 'start'; break;
						case 'a': action = 'A'; break;
						case 'b': action = 'B'; break;
						
						case 'left':
						case 'right':
						case 'up':
						case 'down':
							action = event.key;
							break;
							
						default: break;
					}
				}
				
				if (action != null) {
					ev = new Event();
					ev.action = action;
					ev.down = down;
					ev.up = !down;
					events.add(ev);
					pressedActions[action] = down;
				}
			}
		}
		
		activeScene.processInput(events, pressedActions);
		activeScene._omghax_raw_events = raw_events;
		activeScene.update();
		activeScene.render(renderCounter++);
		activeScene = activeScene.next;
		
		window.clockTick();
	}
}
