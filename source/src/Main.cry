import Game;
import Gamepad;
import Math;

const FPS = 30;
const WIDTH = 870;
const HEIGHT = 672;

const GAME_WIDTH = 256;
const GAME_HEIGHT = 224;
const SOUND_ENABLED = false && $var['sound_enabled'];

// Skips intro cutscene
const DEBUG_MODE = false;

function main() {
	window = new Game.GameWindow('Space Squirrel', FPS, 256, 224, WIDTH, HEIGHT);
	GamepadManager.refreshDevices();
	GamepadManager.restoreSettingsFromUserData(['active']);
	
	activeScene = new OpeningScene();
	renderCounter = 0;

	pressedActions = {};
	for (action : 'left up down right A B start'.split(' ')) {
		pressedActions[action] = false;
	}
	
	events = [];
	while (activeScene != null) {
		events.clear();
		
		quitAttempt = false;
		raw_events = window.pumpEvents();
		for (event : raw_events) {
			down = false;
			action = null;
			switch (event.type) {
				case EventType.QUIT:
					return;
				
				case EventType.GAMEPAD:
					action = event.buttonId;
					down = event.value == true;
					break;
					
				case EventType.KEY_DOWN:
				case EventType.KEY_UP:
					down = event.down;
					switch (event.key) {
						case Game.KeyboardKey.ENTER:
						case Game.KeyboardKey.SPACE:
							action = 'start';
							break;
						case KeyboardKey.A: action = 'A'; break;
						case KeyboardKey.B: action = 'B'; break;
						case KeyboardKey.LEFT: action = 'left'; break;
						case KeyboardKey.RIGHT: action = 'right'; break;
						case KeyboardKey.UP: action = 'up'; break;
						case KeyboardKey.DOWN: action = 'down'; break;
						default: break;
					}
					break;
			}
				
			if (action != null) {
				events.add(new MyEvent(action, down));
				pressedActions[action] = down;
			}
		}
		
		activeScene.processInput(events, pressedActions);
		activeScene._omghax_raw_events = raw_events;
		activeScene.update();
		activeScene.render(renderCounter++);
		activeScene = activeScene.next;
		
		window.clockTick();
	}
}
