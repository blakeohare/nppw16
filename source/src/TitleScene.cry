import Graphics2D;

class TitleScene : AbstractScene {
	field next = this;
	field flags = '';
	field flash_counter = 0;
	field options = [
		['start', 'Start Game'],
		['password', "Enter Password"],
	];
	field index = 0;
	
	constructor() : base() { }
	
	function processInput(events, pressedKeys) {
		for (event : events) {
			if (this.flash_counter < 0) {
				if (event.down) {
					switch (event.action) {
						case 'start':
						case 'A':
						case 'B':
							this.flash_counter = 30;
							playNoise('menu_select');
							break;
						
						case 'up':
							if (this.index > 0) {
								this.index--;
								playNoise('menu_move');
							}
							break;
						
						case 'down':
							if (this.index < 2) {
								this.index++;
								playNoise('menu_move');
							}
							break;
						default:
							break;
					}
				}
			}
		}
	}
	
	function update() {
		this.flash_counter--;
		if (this.flash_counter == 1) {
			action = this.options[this.index][0];
			if (action == 'start')
				this.next = new IntroScene();
			else if (action == 'password')
				this.next = new PasswordScene();
			else
				Core.assert(false, "Unknown option");
		}
	}
	
	function render(renderCounter) {
		JukeBox.ensureSong('title');
		
		Graphics2D.Draw.fill(0, 0, 0);
		
		ImageLibrary.get('slides/title.png').draw(0, 0);
		
		showText = true; // (renderCounter // 10) % 3 != 0;
		if ( this.flash_counter > 0)
			showText = renderCounter % 4 < 2;
		
		index = 0;
		y = 136;
		left = 64;
		
		drawBox(left - 32, y - 16, 24, 7);
		for (option : this.options) {
			if (index == this.index) {
				drawText(255, 255, 255, ">", left - 16, y);
			}
			if (index != this.index || showText)
				drawText(255, 255, 255, option[1], left, y);
			index++;
			y += 16;
		}
	}
}
