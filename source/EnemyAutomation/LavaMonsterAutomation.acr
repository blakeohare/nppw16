const int RISE_SPEED = 1;
const int WAIT_INTERVAL = FPS * 4;

class LavaMonsterAutomation : AbstractAutomation {
	private int counter = 0;
	private string mode ='wait'; // { wait | rise | shoot | fall }
	public bool mouthOpen = false;
	
	constructor(Sprite sprite) : base(sprite) { }
	
	public override void doStuff(PlayScene scene) {
		this.counter++;
		this.mouthOpen = false;
		bool faceLeft = scene.player.x < this.sprite.x;
		this.sprite.lastDirection = faceLeft ? 'left' : 'right';
		if (this.mode == 'wait') {
			if (this.counter == WAIT_INTERVAL) {
				this.counter = 0;
				this.mode = 'rise';
			}
		} else if (this.mode == 'rise') {
			if (this.counter == 16) {
				this.mode = 'shoot';
				this.counter = 0;
			}
			this.sprite.dy = -1;
		} else if (this.mode == 'shoot') {
			if (this.counter == 60) {
				this.mode = 'fall';
				this.counter = 0;
			}
			if (this.counter == 7) {
				this.createFireBall();
			}
			if (this.counter > 0 && this.counter < 14) {
				this.mouthOpen = true;
			}
		} else if (this.mode == 'fall') {
			if (this.counter == 16) {
				this.mode = 'wait';
				this.counter = 0;
			}
			this.sprite.dy = 1;
		}
	}
		
	public void createFireBall() {
		Sprite fireball = new Sprite('fireball', this.sprite.x, this.sprite.y);
		this.sprite.spawns = new List<Sprite>() { fireball };
	}
}
