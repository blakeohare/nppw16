import Math;

const int JUMPING_VY = -14;
const int RUN_JUMPING_VY = -16;
const int WATER_JUMPING_VY = -5;
const float G = 0.7;
const float STRONG_G = 1.55;
const float WATER_G = 0.1;

public int getSpriteHeight(string type) {
	return Sprite.SPRITE_HEIGHT.get(type, 16);
}

public class Sprite {
	public string type;
	public bool isEnemy = false;
	public int height;
	public int x;
	public int y;
	public float modelX;
	public float modelY;
	public float vy = 0;
	public bool moving = false;
	public string lastDirection = 'left';
	public bool onGround = false;
	public List<List<int>> neighbors = [null] * 36;
	public List<Sprite> spawns = null;
	public bool dead = false;
	public bool collidedWall = false;
	public bool deleteWhenOffScreen = false;
	public bool ghost = false;
	field renderImpl = SPRITE_renderPlayerOver;
	field automation = null;
	field floats = false;
	field deathState = null;
	field sprinkle = false;
	field isBullet;
	field inBackground = false;
	field bikemode = false;
	field powerupInfo = null;
	field acorntopdir;
	field dx = 0;
	field dy = 0;
	field iceVX = 0;
	field ddx = 0;
	field damageDir = null;
	field cling = false;
	field ladderDY = 0;
	field blinkCounter = -1;
	field cameFromIce = false;
	field bvx;
	field goLeft;
	
	constructor(type, px, py) {
		this.type = type;
		this.height = getSpriteHeight(type);
		this.x = px;
		this.y = py;
		this.modelX = px + 0.0;
		this.modelY = py + 0.0;
		
		this.isEnemy = Sprite.ENEMY_TYPES.contains(type);
		this.isBullet = type == 'lazor';
		
		switch (type) {
			case 'acorn':
				this.renderImpl = SPRITE_renderAcorn;
				this.automation = new AcornAutomation(this);
				break;
			case 'acorntop':
				this.renderImpl = SPRITE_renderAcornTop;
				this.automation = new AcornTopAutomation(this);
				this.acorntopdir = 'left';
				break;
			case 'gravity_device':
				this.renderImpl = SPRITE_renderGravityDevice;
				this.automation = null;
				break;
			case 'lazor':
				this.renderImpl = SPRITE_renderLazor;
				this.automation = new LazorAutomation(this);
				break;
			case 'lavamonster':
				this.renderImpl = SPRITE_renderLavaMonster;
				this.automation = new LavaMonsterAutomation(this);
				this.inBackground = true;
				this.floats = true;
				this.ghost = true;
				break;
			case 'fireball':
				this.renderImpl = SPRITE_renderFireball;
				this.automation = new FireBallAutomation(this);
				this.floats = true;
				// not a ghost, though. Collides with walls and that triggers it to fizzle.
				break;
			case 'waterpop':
				this.renderImpl = SPRITE_renderWaterPop;
				this.automation = new WaterPopAutomation(this);
				this.floats = true;
				break;
			case 'powerup':
				this.renderImpl = SPRITE_renderPowerup;
				this.automation = new PowerupAutomation(this);
				break;
			case 'poof_big':
				this.renderImpl = SPRITE_renderPoof;
				this.automation = new PoofAutomation(this, true);
				this.floats = true;
				break;
			case 'poof_small':
				this.renderImpl = SPRITE_renderPoof;
				this.automation = new PoofAutomation(this, false);
				this.floats = true;
				break;
			case 'byat':
				this.renderImpl = SPRITE_renderByat;
				this.automation = new ByatAutomation(this);
				this.floats = true;
				break;
			case 'moonsquid':
				this.renderImpl = SPRITE_renderMoonSquid;
				this.automation = new MoonSquidAutomation(this);
				this.floats = true;
				break;
			case 'seeds':
				this.renderImpl = SPRITE_renderSeed;
				this.automation = new SeedAutomation(this);
				break;
			case 'bird1':
				this.renderImpl = SPRITE_renderBird;
				this.automation = new BirdAutomation(this, false);
				this.floats = true;
				break;
			case 'bird2':
				this.renderImpl = SPRITE_renderBird;
				this.automation = new BirdAutomation(this, true);
				this.floats = true;
				this.ghost = true;
				break;
			case 'rocketbike':
				this.renderImpl = SPRITE_renderRocketBike;
				this.automation = new RocketBikeAutomation(this);
				this.ghost = true;
				break;
			default:
				break;
		}
	}
	
	static field NEIGHBOR_RANGE = [-1, 0, 1];

	static field SPRITE_HEIGHT = {
		'player_side': 32,
		'acorn': 16,
		'acorntop': 16,
		'gravity_device': 16,
		'lazor': 16,
		'lavamonster': 32,
		'waterpop': 32,
		'powerup': 16,
		'poof_big': 32,
		'poof_small': 16,
		'byat': 16,
		'moonsquid': 16,
		'seed': 16,
		'rocketbike': 32,
	};

	static field XY_PAIRINGS = [
		[0, 0],
		[1, 0],
		[2, 0],
		[3, 0],
		[4, 0],
		[4, 1],
		[4, 2],
		[4, 3]
	];

	static field ENEMY_TYPES = [
		'acorn',
		'acorntop'
	];

	function checkNeighborCollision(scene, col, row, targetX, targetY) {
		area_left = targetX - 5;
		area_right = targetX + 5;
		area_top = targetY - 5;
		area_bottom = targetY + 5;
		
		width = scene.cols;
		height = scene.rows;
		left = col - 2;
		right = col + 2;
		top = row - 2;
		bottom = row + 2;
		if (left < 0) left = 0;
		if (right >= width) right = width - 1;
		if (top < 0) top = 0;
		if (bottom >= height) bottom = height - 1;
		index = 0;
		for (y = top; y <= bottom; ++y) {
			for (x = left; x <= right; ++x) {
				cs = scene.tiles[x][y].collisions;
				for (csi = cs.length - 1; csi >= 0; --csi) {
					this.neighbors[index] = cs[csi];
					index++;
				}
			}
		}
		
		index--;
		while (index >= 0) {
			n = this.neighbors[index];
			// WAT? TODO: Clean this up, plz.
			if (n[0] > area_right) { }
			else if (n[1] > area_bottom) { }
			else if (n[2] < area_left) { }
			else if (n[3] < area_top) { }
			else {
				return false;
			}
			--index;
		}
		return true;
	}
		
	function update(scene) {
		if (this.automation != null) {
			this.automation.doStuff(scene);
		}
		
		--this.blinkCounter;
		
		if (this.blinkCounter < 0 || this.onGround) {
			this.ddx = 0;
			if (this.damageDir != null) {
				this.lastDirection = this.damageDir;
			}
			this.damageDir = null;
		}
		
		if (this.damageDir != null) {
			this.dx = 0;
		}
		
		this.dx += this.ddx;
		
		if (scene.side) {
			// hotspot is located in the center of the bottom most tile
			areaX = this.modelX;
			areaBottom = this.modelY + 8;
			heightFromHotSpot = getSpriteHeight(this.type) - 8 - 4;
			areaTop = this.modelY - heightFromHotSpot;
			waterOnly = this.type == 'moonsquid';
			
			width = scene.cols;
			height = scene.rows;
			
			isPlayer = this == scene.player;
			bikePhysics = isPlayer && scene.id == 'bike_level';
			
			if (bikePhysics) {
				offsetX = -(scene.updateCounter * BIKE_SPEED);
				if (offsetX > 0) offsetX = 0;
				right = -(scene.cols * 16 - 256);
				if (offsetX < right) offsetX = right;
				
				newX = areaX + this.dx;
				pixelX = newX + offsetX;
				if (pixelX < 8)
					this.dx = BIKE_SPEED;
				else if (pixelX > 248)
					this.dx = BIKE_SPEED;
			}	
			
			tileBottomIceCheck = floor(areaBottom / 16);
			tileX = floor(areaX / 16);
			inWater = false;
			onIce = false;
			if (tileX >= 0 && tileBottomIceCheck > 0 && tileX < scene.cols && tileBottomIceCheck < scene.rows) {
				tileBelow = scene.tiles[tileX][tileBottomIceCheck];
				if (tileBelow != null) {
					onIce = tileBelow.isIce;
				}
				
				inWater = scene.tiles[tileX][tileBottomIceCheck - 1].isWater;
			}
			
			// TODO: move these to global area
			ICE_DIMINISH = .1;
			ICE_MAX_SPEED = 8;
			
			if (this.type != 'lazor' && (onIce || (this.cameFromIce && !this.onGround && !this.cling))) {
				
				if (this.dx == 0) {
					if (this.iceVX > 0) {
						this.iceVX -= ICE_DIMINISH;
						if (this.iceVX < 0) {
							this.iceVX = 0;
						}
					} else if (this.iceVX < 0) {
						this.iceVX += ICE_DIMINISH;
						if (this.iceVX > 0) {
							this.iceVX = 0;
						}
					}
				} else if (this.dx > 0) {
					this.iceVX += ICE_DIMINISH;
				} else if (this.dx < 0) {
					this.iceVX -= ICE_DIMINISH;
				}
				
				this.dx = this.iceVX;
				if (this.dx > ICE_MAX_SPEED) {
					this.dx = ICE_MAX_SPEED;
				} else if (this.dx < -ICE_MAX_SPEED) {
					this.dx = -ICE_MAX_SPEED;
				}
				this.cameFromIce = true;
			} else {
				this.iceVX = 0;
				if (this.onGround) {
					this.cameFromIce = false;
				}
			}
			
			this.moving = this.dx != 0 || this.dy != 0;
			this.collidedWall = false;
			// side-to-side calcuation is done first, independent of whether you are on the ground.
			if (this.dx != 0) {
				if (inWater && isPlayer) {
					maxDX = this.onGround ? 1.0 : 1.5;
					if (this.dx < -maxDX) {
						this.dx = -maxDX;
					} else if (this.dx > maxDX) {
						this.dx = maxDX;
					}
				}
					
				newX = this.modelX + this.dx;
				
				// isCollision ignores collisions near the sprite's feet
				// if you are near ground, you will be automatically placed standing
				// upon it at the end of the vertical adjustment phase.
				if (this.ghost || !scene.isCollision(newX, areaTop, newX, areaBottom)) {
					if (this.cling) {
						if (floor(newX / 16) != floor(this.modelX / 16)) {
							newX = this.modelX;
						}
					}
					this.modelX = newX;
					if (this.dx > 0) {
						this.lastDirection = 'right';
					} else {
						this.lastDirection = 'left';
					}
				} else {
					this.iceVX = 0;
					this.collidedWall = !this.ghost;
				}
				this.dx = 0;
			}
			
			tileX = floor(this.modelX / 16);
			tileY = floor(this.modelY / 16);
			
			offScreen = tileX < 0 || tileX >= scene.cols || tileY < 0 || tileY >= scene.rows;
			onScreen = !offScreen;
			
			if (bikePhysics && this.collidedWall) {
				scene.next = new DeathOverrideScene(scene, 'lava');
			}
			
			// vertical adjustment phase
			// assume sprite is flying through the air unless you see ground
			wasOnGround = this.onGround; // save the previous ground state. If you weren't on ground before but suddenly are, then you "landed" and a sound should be played. 
			this.onGround = false;
			
			if (wasOnGround || this.floats) {
				this.vy = 0;
			} else {
				if (inWater) {
					this.vy += WATER_G;
				} else if (scene.context.gravity) {
					this.vy += STRONG_G;
				} else {
					this.vy += G;
				}
			}
			
			rocketJump = false;
			if (inWater && isPlayer && this.vy < 0) {
				tileY = floor(this.modelY / 16) - 1;
				if (tileY >= 0) {
					if (!scene.tiles[tileX][tileY].isWater) {
						// moving up, bottom half is in water, top half is not
						rocketJump = true; // jump out with high velocity
					}
				}
			}
			
			// If you're clinging to a ladder, then throw out the vy entirely.
			// Use this.ladderDY instead
			if (this.cling) {
				this.dy = this.ladderDY;
				this.vy = 0;
				if (this.dy != 0) {
					this.moving = true;
				}
			} else {
				this.dy += this.vy;
			}
			
			oldLadderDY = this.ladderDY;
			this.ladderDY = 0;
			
			maxDY = 10;
			if (this.dy > 4 && inWater) {
				this.dy = 4;
			}
			
			if (this.dy > maxDY) {
				this.dy = maxDY;
			} else if (this.dy < -maxDY) {
				this.dy = -maxDY;
			}
			
			if (rocketJump) {
				this.dy = -15;
			}
			
			wasCling = this.cling;
			movedUp = false;
			
			newBottom = areaBottom + this.dy;
			newTop = areaTop + this.dy;
			
			no = false;
			
			if (this.ghost) {
				// just go with it
			} else {
				if (this.dy < 0) {
					// Going Up
					if (newTop < 0) {
						// TODO: do top transitions here.
						no = true;
					} else {
						newTileTop = floor(newTop / 16);
						
						offScreen = tileX < 0 || tileX >= scene.cols || newTileTop < 0 || newTileTop >= scene.rows;
						onScreen = !offScreen;
						
						if (onScreen) {
							t = scene.tiles[tileX][newTileTop];
							if (t.solid || (waterOnly && !t.isWater)) {
								if (wasOnGround) {
									this.onGround = true;
								}
								no = true;
								
								if (this.modelY % 16 == 8) {
									no = false;
								}
								this.vy = 0;
								this.dy = 0;
								if (this == scene.player) {
									playNoise('head_bonk');
								}
							} else {
								movedUp = true;
							}
						} else {
							movedUp = true;
						}
					}
				} else {
					// Going Down (or staying the same)
					// The philosophy here is different. When you encounter a collision,
					// rather than stop the movement, you find where the top of the collided
					// tile is and place the sprite there.
					
					newTileBottom = floor(newBottom / 16);
					
					// If the player fell off the bottom
					if (isPlayer && newTileBottom >= height) {
						// TODO: check if there is a transition registered with the map.
						// If so begin that transition instead of killing the player.
						playNoise('fall_to_death');
						scene.next = new DeathOverrideScene(scene, 'fall');
						no = true;
					} else {
						offScreen = tileX < 0 || tileX >= scene.cols || newTileBottom < 0 || newTileBottom >= scene.rows;
						onScreen = !offScreen;
						
						if (onScreen) {
							t = scene.tiles[tileX][newTileBottom];
							topStop = false;
							if (t.isTop) {
								oldTileBottom = floor(areaBottom / 16);
								if (newTileBottom > oldTileBottom) {
									topStop = true;
								} else if (newTileBottom == oldTileBottom && areaBottom == newBottom) {
									topStop = true;
								}
							}
							
							if (t.solid || topStop) {
								no = true;
								this.cling = false;
								this.onGround = true;
								this.modelY = newTileBottom * 16 - 8;
								this.vy = 0;
								if (!wasOnGround && this == scene.player) {
									playNoise('land_on_ground');
								}
							}
						} else {
							no = true;
						}
					}
				}
			}
					
			if (!no) {
				this.modelY = newBottom - 8;
			}
			
			this.x = floor(this.modelX);
			this.y = floor(this.modelY);
			this.dy = 0; // TODO: isn't this supposed to be this.dx?
			this.dy = 0;
			
			tileY = floor(this.modelY / 16);
			
			offScreen = tileX < 0 || tileX >= scene.cols || tileY < 0 || tileY >= scene.rows;
			onScreen = !offScreen;
			if (onScreen && scene.tiles[tileX][tileY].isLadder) {
				// TODO: why are there so many inverted expressions like this?
			} else {
				this.cling = false;
			}
			
			if (movedUp && wasCling && !this.cling) {
				this.modelY = 16 * tileY; // maybe?
			}
		} else {
			this.moving = false;
			if (this.dx != 0 || this.dy != 0) {
				this.moving = true;
				
				if (this.dy < 0) {
					this.lastDirection = 'up';
				} else if (this.dy > 0) {
					this.lastDirection = 'down';
				} else if (this.dx < 0) {
					this.lastDirection = 'left';
				} else {
					this.lastDirection = 'right';
				}
					
				
				// TODO: this is silly. Break the vector down into horizontal and vertical
				// components and do a TryMove approach.
				List<float> xs = Sprite.xs;
				List<float> ys = Sprite.ys;
				
				xs[0] = this.modelX + this.dx;
				xs[1] = this.modelX + this.dx * 3.0 / 4;
				xs[2] = this.modelX + this.dx / 2.0;
				xs[3] = this.modelX + this.dx / 4.0;
				xs[4] = this.modelX;
				
				ys[0] = this.modelY + this.dy;
				ys[1] = this.modelY + this.dy * 3.0 / 4;
				ys[2] = this.modelY + this.dy / 2.0;
				ys[3] = this.modelY + this.dy / 4.0;
				
				for (List<int> pairing : Sprite.XY_PAIRINGS) {
					newx = xs[pairing[0]];
					newy = ys[pairing[1]];
					col = floor(newx / 16);
					row = floor(newy / 16);

					if (this.checkNeighborCollision(scene, col, row, newx, newy)) {
						this.modelX = newx;
						this.modelY = newy;
						this.x = floor(this.modelX);
						this.y = floor(this.modelY);
						break;
					}
				}
				
				this.dx = 0;
				this.dy = 0;
			}
		}
	}
	
	private static List<float> xs = new List<float>() { 0.0 } * 5;
	private static List<float> ys = new List<float>() { 0.0 } * 4;
	
	function render(scene, offsetX, offsetY, rc) {
		if (this.blinkCounter < 0 || (this.blinkCounter & 1) == 0) {
			this.renderImpl(this, scene, offsetX, offsetY, rc);
		}
	}
	
	function isCollision(other) {
		left = this.x - 8;
		oleft = other.x - 8;
		right = left + 16;
		oright = oleft + 16;
		
		if (left >= oright || right <= oleft) {
			return false;
		}
		
		bottom = this.y + 8;
		obottom = other.y + 8;
		top = bottom - this.height;
		otop = obottom - other.height;
		
		if (top >= obottom || otop >= bottom)
			return false;
		return true;
	}
	
	function hit(scene, amount) {
		this.blinkCounter = 40;
		scene.context.adjustHealth(scene, -amount);
		
		if (scene.id != 'bike_level') {
			tiles = scene.tiles;
			tx = this.x / 16;
			ty = this.y / 16;
			
			inWater = false;
			if (tx >= 0 && ty >= 0 && tx < scene.cols && ty < scene.rows) {
				t = tiles[tx][ty];
				inWater = t.isWater;
			}
			
			this.damageDir = this.lastDirection;
			if (this.lastDirection == 'left') {
				this.ddx = 3;
			} else {
				this.ddx = -3;
			}
			this.onGround = false;
			if (inWater) {
				this.vy = -1;
			} else {
				this.vy = -6;
			}
		}
	}
}
