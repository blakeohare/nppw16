import Math;

class VolcanoCoreActivation : SpecialLevelStuff {
	field id;
	field spotX = 11;
	field spotY = 9;
	field sequenceCounter = -1;
	field done = false;
	field warpBack = false;
	
	constructor(scene, id) : base(scene) {
		this.id = id;
		this.scene = scene;
		this.context = scene.context;
		this.hasUpdate = true;
		this.hasPostInit = true;
		this.lavaLevel = 0;
		this.shakeScreen = false;
		this.freeze = false;
	}
		
	function update() {
		player = this.scene.player;
		tx = player.x / 16;
		ty = player.y / 16;
		
		if (tx == this.spotX && Math.abs(ty - this.spotY) < 2 && !this.done) {
			this.doLavaSequence(false);
		}
		
		this.freeze = false;
		this.shakeScreen = false;
		this.scene.player.sprinkle = false;
		
		if (this.sequenceCounter >= 0) {
			SPRINKLE = 30; // player sprinkles mix
			SHAKE = 60; // screenshakes 
			RISE_RATE = 30; // this happens 3 times
			
			this.freeze = true;
			sc = this.sequenceCounter;
			if (sc < SPRINKLE) {
				this.scene.player.sprinkle = true;
				if (sc == 0) {
					playNoise('sprinkle_lava_packet');
				}
				progress = 1.0 * sc / SPRINKLE;
			} else {
				sc -= SPRINKLE;
				if (sc < SHAKE) {
					if (sc == 0) {
						playNoise('screen_shaking');
					}
					this.shakeScreen = (sc & 1) == 0;
				} else {
					sc -= SHAKE;
					if (sc < RISE_RATE) {
						if (sc == 0) {
							playNoise('lava_rise');
						}
						this.lavaLevel = 1;
					} else {
						sc -= RISE_RATE;
						if (sc < RISE_RATE) {
							if (sc == 0) {
								playNoise('lava_rise');
							}
							this.lavaLevel = 2;
						} else {
							sc -= RISE_RATE;
							this.lavaLevel = 3;
							if (sc < RISE_RATE) {
								if (sc == 0) {
									playNoise('lava_rise');
								}
							} else {
								this.freeze = false;
								if (this.warpBack) {
									this.scene.next = new VolcanoCompleteScene(this.scene, this.context);
								}
							}
						}
					}
				}
			}
			this.sequenceCounter += 1;
		}
	}

	function postInit() {
		isLavaOn = false;
		if (this.id == 1) {
			isLavaOn = this.context.volcanoA;
		} else if (this.id == 2) {
			isLavaOn = this.context.volcanoB;
		} else if (this.id == 3) {
			isLavaOn = this.context.volcanoC;
		}
		
		if (isLavaOn) {
			this.doLavaSequence(true);
		}
	}
	
	function doLavaSequence(instant) {
		this.done = true;
		if (instant) {
			this.sequenceCounter = 9999999;
		} else {
			this.sequenceCounter = 0;
		}
		
		complete = this.context.volcanoA && this.context.volcanoB && this.context.volcanoC;
		
		if (!complete) {
			if (this.id == 1) {
				this.context.volcanoA = true;
			} else if (this.id == 2) {
				this.context.volcanoB = true;
			} else if (this.id == 3) {
				this.context.volcanoC = true;
			}
			
			complete = this.context.volcanoA && this.context.volcanoB && this.context.volcanoC;
			if (complete) {
				this.warpBack = true;
			}
		}
	}
}
